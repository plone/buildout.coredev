*** Settings ***
Resource  plone.txt

Suite setup  Setup

Suite teardown  Remove Pages

*** Test cases ***

Copy Element
    Go to homepage
    Click Contents In Edit Bar

    Select Checkbox  css=#cb_test1
    Click Button  Copy
    Page Should Contain  1 item(s) copied.
    Click Button  Paste
    Page Should Contain Element  css=#folder-contents-item-copy_of_test1

Delete Element
    Go to homepage
    Click Contents In Edit Bar

    Select Checkbox  css=#cb_copy_of_test1
    Click Button  Delete
    Page Should Contain  Item(s) deleted.
    Page Should Not Contain Element  css=#folder-contents-item-copy_of_test1    

Reorder Folder Contents
    Go to homepage
    Click Contents In Edit Bar

    # Moves the test2 page above the test1 page
    Should be above  css=tr#folder-contents-item-test1  css=tr#folder-contents-item-test2
    Reorder Element  folder-contents-item-test1  folder-contents-item-test2
    Should be above  css=tr#folder-contents-item-test2  css=tr#folder-contents-item-test1

    # Moves the test4 page above the test2 page
    Reorder Element  folder-contents-item-test4  folder-contents-item-test3
    Reorder Element  folder-contents-item-test4  folder-contents-item-test1
    Reorder Element  folder-contents-item-test4  folder-contents-item-test2

    # Moves the test3 page above the test2 page
    Reorder Element  folder-contents-item-test3  folder-contents-item-test1
    Reorder Element  folder-contents-item-test3  folder-contents-item-test2

    # The new order is 4 > 3 > 2 > 1
    Should be above  css=tr#folder-contents-item-test4  css=tr#folder-contents-item-test3
    Should be above  css=tr#folder-contents-item-test3  css=tr#folder-contents-item-test2
    Should be above  css=tr#folder-contents-item-test2  css=tr#folder-contents-item-test1

*** Keywords ***

Setup
    Log in  admin  admin
    Create Pages

Create Pages
    Go to homepage
    Add Page  test1
    Go to homepage
    Add Page  test2
    Go to homepage
    Add Page  test3
    Go to homepage
    Add Page  test4
    Go to homepage

Remove Pages
    Remove Content  test1
    Remove Content  test2
    Remove Content  test3
    Remove Content  test4

Reorder Element
    [arguments]  ${element}  ${destination}

    Mouse Down  css=#${element} td.draggable
    Mouse Move  css=#${destination} td.draggable
    Mouse Up  css=#${element} td.draggable
    Mouse Out  css=#${element} td.draggable

